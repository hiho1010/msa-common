plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.bangbang'
version = '0.1.0'

repositories {
    mavenCentral()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = null
}

ext {
    set('springCloudVersion', "2025.0.0")
    set('resilience4jVersion', "2.3.0")
    set('springDocVersion', "2.8.14")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "io.github.resilience4j:resilience4j-bom:${resilience4jVersion}"
    }
}

dependencies {
    // --- [1] 공통 스택 ---
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-aop'
    api 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    api 'org.springframework.boot:spring-boot-starter-json'
    api 'org.springframework.kafka:spring-kafka'

    // --- [2] Spring Cloud ---
    api 'org.springframework.cloud:spring-cloud-starter-config'
    api 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    api 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    api 'org.springframework.cloud:spring-cloud-starter-openfeign'
    api 'org.springframework.cloud:spring-cloud-starter-stream-kafka'

    // --- [3] Resilience4j ---
    api 'io.github.resilience4j:resilience4j-spring-boot3'
    api 'io.github.resilience4j:resilience4j-circuitbreaker'
    api 'io.github.resilience4j:resilience4j-retry'
    api 'io.github.resilience4j:resilience4j-bulkhead'
    api 'io.github.resilience4j:resilience4j-ratelimiter'
    api 'io.micrometer:micrometer-core'

    // --- [4] 유틸리티 및 문서화 ---
    api 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    api 'com.fasterxml.jackson.core:jackson-databind'

    api 'io.micrometer:micrometer-tracing'
    api 'io.micrometer:micrometer-tracing-bridge-brave'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                classifier = null
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}